import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        mavenCentral()
        google()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://maven.aliyun.com/nexus/content/repositories/central/' }
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.4.32'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id("com.github.gmazzo.buildconfig") version("2.0.2")
}

def mainClass = 'io.github.starwishsama.comet.CometApplication'
def miraiVersion = '2.5.0'

jar {
    manifest {
        attributes 'Main-Class': mainClass
        attributes 'Author': "StarWishsama"
    }
}

String branchName = 'git symbolic-ref --short -q HEAD'.execute().text.trim()
String gitBranchName = "-" + (branchName == null && branchName.isEmpty() ? "Unofficial" : branchName)
String gitCommitId = 'git rev-parse --short HEAD'.execute().text.trim()
String gitCommitIdResult = (gitCommitId == null && gitCommitId.isEmpty() ? "" : "-" + gitCommitId)
String versionCode = "0.6.1" + gitBranchName + gitCommitIdResult

group 'io.github.starwishsama.comet'
version versionCode

tasks {
    buildConfig {
        println "Processing version code and other stuffs....."

        packageName("io.github.starwishsama.comet")
        buildConfigField("String", "version", "\"${versionCode}\"")
        buildConfigField("String", "buildTime", "\"${LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyy/MM/dd HH:mm:ss"))}\"")
        buildConfigField("String", "miraiVersion", "\"$miraiVersion\"")
    }
}

repositories {
    mavenCentral()
    google()
    jcenter()
    maven { url 'http://maven.aliyun.com/nexus/content/repositories/central/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-core:1.1.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    implementation("net.mamoe:mirai-core-all:$miraiVersion")

    implementation 'cn.hutool:hutool-http:5.6.0'
    implementation 'cn.hutool:hutool-crypto:5.6.0'

    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-io:1.3.2'

    // yamlkt @ https://github.com/him188/yamlkt
    implementation("net.mamoe.yamlkt:yamlkt:0.9.0-dev-1")
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.2'
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.12.2"
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.2'

    // CUrl
    implementation 'com.github.rockswang:java-curl:1.2.2.190107'

    // BiliBili-Api @ https://github.com/czp3009/bilibili-api
    api 'com.hiczp:bilibili-api:0.1.3'

    // jsoup HTML parser library @ https://jsoup.org/
    implementation 'org.jsoup:jsoup:1.13.1'

    // Retrofit A type-safe HTTP client for Android and Java @ https://github.com/square/retrofit/
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'

    // RomeTool for RSS @ https://github.com/rometools/rome
    implementation 'com.rometools:rome:1.15.0'
    implementation 'com.rometools:rome-utils:1.15.0'
    implementation 'com.rometools:rome-fetcher:1.15.0'

    // Jline a Java library for handling console input @ https://github.com/jline/jline3
    implementation 'org.jline:jline:3.19.0'

    // Jansi needed by JLine
    implementation 'org.fusesource.jansi:jansi:2.3.2'

    // DNSJava used to srv lookup
    implementation 'dnsjava:dnsjava:3.3.1'
}

shadowJar {
    dependsOn(generateBuildConfig)
    zip64 true
    exclude("META-INF/*.txt")
    exclude("META-INF/*.md")
    exclude("META-INF/CHANGES")
    exclude("META-INF/LICENSE")
    exclude("META-INF/NOTICE")
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs = ["-Xopt-in=kotlin.RequiresOptIn", "-XXLanguage:+InlineClasses"]
    }
}
